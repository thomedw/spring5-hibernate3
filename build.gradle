buildscript {
	repositories {
		maven { url "https://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
		classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.2")
		classpath("io.spring.gradle:docbook-reference-plugin:0.3.1")
		classpath("ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.9.1")
	}
	ext.springVersion = '5.0.2.RELEASE'

	ext.aspectjVersion = "1.8.9"
	ext.eclipselinkVersion = "2.4.2"
	ext.hamcrestVersion = "1.3"
	ext.hibernate3Version = "3.6.10.Final"
	ext.hsqldbVersion = "2.3.4"
	ext.junitVersion = "4.12"
	ext.openjpaVersion = "2.4.2"
	ext.slf4jVersion = "1.7.21"
	ext.snifferVersion = "1.15"
}
plugins {
	id "com.jfrog.bintray" version "1.7.3"
}
group 'com.katalisindonesia.spring5'
version '0.0.1'

apply plugin: "propdeps"
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
}

repositories {
	jcenter()
	mavenCentral()
	maven {
		url "http://maven.katalisindonesia.com/repo"
		credentials {
			username "$mavenUser"
			password "$mavenPassword"
		}
	}
}
dependencies {
	optional "org.springframework:spring-context:${springVersion}"
	optional "org.springframework:spring-jdbc:${springVersion}"
	optional "org.springframework:spring-orm:${springVersion}"
	optional "org.springframework:spring-web:${springVersion}"
	optional "org.springframework:spring-beans:${springVersion}"
	optional "org.springframework:spring-tx:${springVersion}"
	optional "org.springframework:spring-aop:${springVersion}"
	testCompile "org.springframework:spring-test:${springVersion}"

	optional("org.eclipse.persistence:javax.persistence:2.0.5")
	optional("org.eclipse.persistence:org.eclipse.persistence.core:${eclipselinkVersion}")
	optional("org.eclipse.persistence:org.eclipse.persistence.jpa:${eclipselinkVersion}") {
		exclude group: 'org.eclipse.persistence', module: 'javax.persistence'
	}
	optional("org.hibernate:hibernate-core:${hibernate3Version}") {
		exclude group: 'org.hibernate.javax.persistence', module: 'hibernate-jpa-2.0-api'
		exclude group: 'javax.transaction', module: 'jta'
	}
	optional("org.hibernate:hibernate-entitymanager:${hibernate3Version}") {
		exclude group: 'org.hibernate.javax.persistence', module: 'hibernate-jpa-2.0-api'
	}
	optional("org.apache.openjpa:openjpa:${openjpaVersion}") {
		exclude group: 'junit', module: 'junit'
		exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jpa_2.0_spec'
		exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jta_1.1_spec'
		exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jms_1.1_spec'
	}
	optional("javax.jdo:jdo-api:3.1") {
		exclude group: 'javax.transaction', module: 'transaction-api'
	}
	optional("javax.servlet:javax.servlet-api:3.0.1")
	testCompile("commons-dbcp:commons-dbcp:1.4")
	testCompile("org.aspectj:aspectjweaver:${aspectjVersion}")
	testCompile("org.hsqldb:hsqldb:${hsqldbVersion}")
	testCompile("org.slf4j:slf4j-jcl:${slf4jVersion}")

	testCompile("junit:junit:${junitVersion}") {
		exclude group: 'org.hamcrest', module: 'hamcrest-core'
	}
	testCompile("org.mockito:mockito-core:1.10.19") {
		exclude group: 'org.hamcrest', module: 'hamcrest-core'
	}
	testCompile("org.hamcrest:hamcrest-all:${hamcrestVersion}")
}

//apply plugin: 'maven-publish'

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier "sources"
	from sourceSets.main.allJava
}

artifacts {
	archives sourcesJar
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	configurations = ['archives']
	publish=true
	override=true
	pkg {
		repo = 'maven'
		name = 'spring5-hibernate3'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/thomedw/spring5-hibernate3.git'

		version {
			name = project.version
		}
	}
}
